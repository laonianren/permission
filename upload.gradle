apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

// 定义两个链接，下面会用到。
def siteUrl = 'https://github.com/laonianren/permission' // 项目主页。
def gitUrl = 'https://github.com/laonianren/permission' // Git仓库的url。

group = "com.github.lnr"// 唯一包名
version = "1.0.0"//项目引用的版本号

install {
    repositories.mavenInstaller {
        // 生成pom.xml和参数
        pom {
            project {
                packaging 'aar'
                // 项目描述
                name 'permission-request'// 可选，项目名称。
                description '权限申请(参考https://github.com/tbruyelle/RxPermissions) 剔除Rx依赖，功能不变'// 可选，项目描述。
                url siteUrl // 项目主页

                // 软件开源协议，现在一般都是Apache License2.0吧
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'lnr' // 开发者的id。
                        name 'lnr' // 开发者名字。
                        email '8376503@gmain.com' // 开发者邮箱。
                    }
                }

                scm {
                    connection gitUrl // Git仓库地址。
                    developerConnection gitUrl // Git仓库地址。
                    url siteUrl // 项目主页。
                }
            }
        }
    }
}

// 生成jar包的task
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

// 这里是读取Bintray相关的信息
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user") // Bintray的用户名。
    key = properties.getProperty("bintray.apikey") // Bintray刚才保存的ApiKey。

    configurations = ['archives']
    pkg {
        repo = "android"  //Repository名字 需要自己在bintray网站上先添加
        name = "permission"// 发布到Bintray上的项目名字，这里的名字不是compile 'com.ansen.library:circleimage:1.0.1'中的circleimage。
        userOrg = 'lnr'//Bintray的组织id
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true // 是否是公开项目。
    }
}